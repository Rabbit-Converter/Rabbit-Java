/*
 * This build file was auto generated by running the Gradle 'init' task
 * by 'yelinaung' at '7/10/15 12:18 AM' with Gradle 2.4
 *
 * This generated file contains a sample Java project to get you started.
 * For more details take a look at the Java Quickstart chapter in the Gradle
 * user guide available at http://gradle.org/docs/2.4/userguide/tutorial_java_projects.html
 */

buildscript {
  repositories {
    jcenter()
  }
}

plugins {
  id "com.jfrog.bintray" version "1.2"
}

allprojects {

  repositories {
    jcenter()
  }
  apply plugin: 'maven'
  apply plugin: 'maven-publish'
  apply plugin: 'java'

  sourceCompatibility = 1.7
  targetCompatibility = 1.7
}



def siteUrl = 'https://github.com/rabbit-Converter/rabbit-Java'
def gitUrl = 'https://github.com/rabbit-Converter/rabbit-Java.git'
group = 'org.rabbit-converter.rabbit'
version = '0.0.2'

// In this section you declare the dependencies for your production and test code
dependencies {
  // The production code uses the SLF4J logging API at compile time
  compile 'org.slf4j:slf4j-api:1.7.12'

  // JSON
  compile 'org.json:json:20140107'

  testCompile 'junit:junit:4.12'
}

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

jar {
  from sourceSets.main.allSource
}

artifacts {
  archives javadocJar
  archives sourcesJar
}

publishing {
  publications {
    MyPublication(MavenPublication) {
      from components.java
      groupId 'org.rabbit-converter.rabbit'
      artifactId 'rabbit'
      version project.version
    }
  }
}


bintray {
  user = System.getenv('BINTRAY_USER')
  key = System.getenv('BINTRAY_KEY')
  publications = ['MyPublication']
  dryRun = false
  publish = true
  pkg {
    repo = 'maven'
    name = 'rabbit'
    desc = 'Rabbit in Java'
    websiteUrl = siteUrl
    issueTrackerUrl = 'https://github.com/rabbit-Converter/rabbit/issues'
    vcsUrl = gitUrl
    licenses = ['MIT']
    labels = ['java', 'myanmar', 'font-converter', 'zawgyi', 'unicode']
    publicDownloadNumbers = true
    //Optional version descriptor
    version {
      name = project.version
      desc = 'optional, version-specific description'
      released = new Date()
      gpg {
        sign = true //Determines whether to GPG sign the files. The default is false
        passphrase = System.getenv('BINTRAY_GPG_PASSWORD')
        //Optional. The passphrase for GPG signing'
      }
      mavenCentralSync {
        sync = false
        //Optional (true by default). Determines whether to sync the version to Maven Central.
        user = 'userToken' //OSS user token
        password = 'paasword' //OSS user password
        close = '1'
        //Optional property. By default the staging repository is closed and artifacts are released to Maven Central. You can optionally turn this behaviour off (by puting 0 as value) and release the version manually.
      }
    }
  }
}

install {
  repositories.mavenInstaller {
    // This generates POM.xml with proper parameters
    pom {
      project {
        packaging 'jar'

        // Add your description here
        name 'rabbit'
        description = 'Rabbit in Java'
        url siteUrl

        // Set your license
        licenses {
          license {
            name 'MIT'
            url 'http://opensource.org/licenses/MIT'
          }
        }
        developers {
          developer {
            id 'yelinaung'
            name 'Ye Lin Aung'
            email 'me@yelinaung.com'
          }
        }
        scm {
          connection gitUrl
          developerConnection gitUrl
          url siteUrl
        }
      }
    }
  }
}

task wrapper(type: Wrapper) {
  gradleVersion = '2.4'
}
